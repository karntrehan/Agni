// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        compileSdk = 28
        buildTools = '28.0.3'
        minSdk = 21
        targetSdk = 28

        /*https://blog.jetbrains.com/kotlin/category/releases/*/
        kotlinVersion = '1.3.50'
        /*https://github.com/Kotlin/kotlinx.coroutines/releases*/
        coroutinesVersion = '1.3.2'
        kotlinDep = [
                gradlePlugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}",
                stdlib      : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlinVersion}",
                coroutines  : "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutinesVersion}",
        ]

        androidDependencies = [
                /*https://developer.android.com/studio/releases/gradle-plugin*/
                gradlePlugin: "com.android.tools.build:gradle:3.5.0",
                /*https://dl.google.com/dl/android/maven2/index.html*/
                appCompat   : "androidx.appcompat:appcompat:1.1.0",
        ]

        /*https://github.com/google/dagger#dagger*/
        daggerVersion = '2.24'
        daggerDep = [
                core    : "com.google.dagger:dagger:${daggerVersion}",
                compiler: "com.google.dagger:dagger-compiler:${daggerVersion}",
        ]

        /*https://dl.google.com/dl/android/maven2/index.html*/
        archComponentsVersion = '2.1.0'
        archComponents = [
                components        : "androidx.lifecycle:lifecycle-extensions:${archComponentsVersion}",
                componentsCompiler: "androidx.lifecycle:lifecycle-compiler:${archComponentsVersion}",
                viewModelKtx      : "androidx.lifecycle:lifecycle-viewmodel-ktx:${archComponentsVersion}",

        ]

        /*https://developer.android.com/jetpack/androidx/releases/room*/
        roomDbVersion = '2.2.0-rc01'
        roomDb = [
                core    : "androidx.room:room-runtime:${roomDbVersion}",
                compiler: "androidx.room:room-compiler:${roomDbVersion}",
                ktx     : "androidx.room:room-ktx:${roomDbVersion}",
                rx      : "androidx.room:room-rxjava2:${roomDbVersion}",
        ]

        /*https://github.com/square/okhttp/blob/master/CHANGELOG.md*/
        okhttpVersion = '4.2.0'
        /*https://github.com/square/retrofit/blob/master/CHANGELOG.md*/
        retrofitVersion = '2.6.1'
        networking = [
                okhttp      : "com.squareup.okhttp3:okhttp:${okhttpVersion}",
                okhttpLog   : "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}",
                /*https://github.com/google/gson/releases*/
                gson        : "com.google.code.gson:gson:2.8.5",
                retrofit    : "com.squareup.retrofit2:retrofit:${retrofitVersion}",
                retrofitRx  : "com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}",
                retrofitGson: "com.squareup.retrofit2:converter-gson:${retrofitVersion}",
        ]

        rx = [
                /*https://github.com/ReactiveX/RxJava/releases*/
                java   : "io.reactivex.rxjava2:rxjava:2.2.12",
                /*https://github.com/ReactiveX/RxAndroid/releases*/
                android: "io.reactivex.rxjava2:rxandroid:2.1.1",
                /*https://github.com/ReactiveX/RxKotlin/releases*/
                kotlin : "io.reactivex.rxjava2:rxkotlin:2.4.0",
        ]

        ui = [
                /*https://dl.google.com/dl/android/maven2/index.html*/
                constraintLayout: 'androidx.constraintlayout:constraintlayout:1.1.3',
                recyclerview    : 'androidx.recyclerview:recyclerview:1.0.0',
                material        : 'com.google.android.material:material:1.0.0',
                /*https://github.com/square/picasso#download*/
                picasso         : "com.squareup.picasso:picasso:2.71828",
                /*https://github.com/JakeWharton/picasso2-okhttp3-downloader#download*/
                picassoOkHttp   : "com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0"
        ]

        other = [
                //multidex: 'androidx.multidex:multidex:2.0.0'
        ]

        roboelectricVersion = '4.0.2'
        testing = [
                junit               : "junit:junit:4.12",
                junitRunner         : "androidx.test.runner.AndroidJUnitRunner",
                mockitoKotlin       : "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0",
                roboelectric        : "org.robolectric:robolectric:${roboelectricVersion}",
                roboelectricMultidex: "org.robolectric:shadows-multidex:${roboelectricVersion}",
                aacTesting          : "androidx.arch.core:core-testing:${archComponentsVersion}",
                roomDb              : "androidx.room:room-testing:${roomDbVersion}",
                coroutines          : "org.jetbrains.kotlinx:kotlinx-coroutines-test:${coroutinesVersion}"
        ]

    }

    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath androidDependencies.gradlePlugin
        classpath kotlinDep.gradlePlugin
    }
}

allprojects {
    repositories {
        google()
        jcenter()

    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
